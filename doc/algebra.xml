<!-- ------------------------------------------------------------------- -->
<!--                                                                     -->
<!--  algebra.xml           XModAlg documentation             Z. Arvasi  -->
<!--                                                        & A. Odabas  -->
<!--  Copyright (C) 2014-2024, Z. Arvasi & A. Odabas,                    -->
<!--  Osmangazi University, Eskisehir, Turkey                            --> 
<!--                                                                     -->
<!-- ------------------------------------------------------------------- -->

<!-- lines to edit for each new version: 73, 121. --> 

<?xml version="1.0" encoding="UTF-8"?> 

<Chapter Label="Algebra">

<Heading>Algebras and their Actions</Heading>

All the algebras considered in this package will be associative 
and commutative. 
Scalars belong to a commutative ring <B>k</B> with <M>1 \neq 0</M>. 
<P/>
<E>(Why not a field?  
A group ring over the integers is not an algebra. [CDW])</E>
<P/>


<Section>

<Heading>Multipliers</Heading> 

A <E>multiplier</E> in a commutative algebra <M>A</M> 
is a function <M>\mu : A \to A</M> such that 
<Display>
\mu(ab) ~=~ (\mu a)b ~=~ a(\mu b) \quad \forall~ a,b \in A. 
</Display> 
The <E>regular multipliers</E> of <M>A</M> are the functions 
<Display>
\mu_a : A \to A ~:~ \mu_ab = ab \quad \forall~ b \in A. 
</Display>
When <M>A</M> has a one, it follows from the defining condition that 
<M>\mu(b1) = (\mu 1)b</M> and so <M>\mu = \mu_a</M> where <M>a = \mu 1</M>. 
Since an ideal <M>I</M> of <M>A</M> is closed under multiplication, 
a multiplier <M>\mu</M> may be restricted to <M>I</M>. 
<P/> 
<B>Question:</B> 
Is there an example of an algebra <M>A</M> <E>without</E> a one 
which has multipliers <E>not</E> of the form <M>\mu_a</M>? 
<P/>

<ManSection>
   <Oper Name="RegularAlgebraMultiplier"
         Arg="A I a" />
<Description> 
This operation defines the multiplier <M>\mu_a : I \to I</M> 
on an ideal <M>I</M> of <M>A</M>. 
</Description> 
</ManSection> 

<Example>
<![CDATA[
gap> A1 := GroupRing( GF(5), Group( (1,2,3,4,5,6) ) );;
gap> SetName( A1, "A1" );
gap> BA1 := BasisVectors( Basis( A1 ) );; 
gap> v := BA1[1] + BA1[3] + BA1[5];
(Z(5)^0)*()+(Z(5)^0)*(1,3,5)(2,4,6)+(Z(5)^0)*(1,5,3)(2,6,4)
gap> I1 := Ideal( A1, [v] );; 
gap> SetName( I1, "I1" );
gap> v1 := BA1[2];
(Z(5)^0)*(1,2,3,4,5,6)
gap> m1 := RegularAlgebraMultiplier( A1, I1, v1 ); 
[ (Z(5)^0)*()+(Z(5)^0)*(1,3,5)(2,4,6)+(Z(5)^0)*(1,5,3)(2,6,4), 
  (Z(5)^0)*(1,2,3,4,5,6)+(Z(5)^0)*(1,4)(2,5)(3,6)+(Z(5)^0)*(1,6,5,4,3,2) ] -> 
[ (Z(5)^0)*(1,2,3,4,5,6)+(Z(5)^0)*(1,4)(2,5)(3,6)+(Z(5)^0)*(1,6,5,4,3,2), 
  (Z(5)^0)*()+(Z(5)^0)*(1,3,5)(2,4,6)+(Z(5)^0)*(1,5,3)(2,6,4) ]
]]>
</Example>

<ManSection>
   <Oper Name="IsAlgebraMultiplier"
         Arg="mu" />
<Description> 
This function tests the condition <M>\mu(ab) = (\mu a)b = a(\mu b)</M> 
for all <M>a,b</M> in the basis for <M>A</M>. 
</Description> 
</ManSection> 

<Example>
<![CDATA[
gap> IsAlgebraMultiplier( m1 ); 
true
gap> one := One( A1 );; 
gap> L1 := List( BA1, v -> one );; 
gap> h1 := LeftModuleHomomorphismByImages( A1, A1, BA1, L1 ); 
[ (Z(5)^0)*(), (Z(5)^0)*(1,2,3,4,5,6), (Z(5)^0)*(1,3,5)(2,4,6), 
  (Z(5)^0)*(1,4)(2,5)(3,6), (Z(5)^0)*(1,5,3)(2,6,4), (Z(5)^0)*(1,6,5,4,3,2) 
 ] -> [ (Z(5)^0)*(), (Z(5)^0)*(), (Z(5)^0)*(), (Z(5)^0)*(), (Z(5)^0)*(), 
  (Z(5)^0)*() ]
gap> IsAlgebraMultiplier( h1 );                                                
false
]]>
</Example>

<ManSection>
   <Oper Name="MultiplierAlgebraOfIdealBySubalgebra "
         Arg="A I B" />
<Description>
The regular multipliers <M>\mu_b : I \to I</M> for all <M>b \in B</M>, 
where <M>I</M> is an ideal in <M>A</M> and <M>B</M> is a subalgebra of <M>A</M>, 
form an algebra with product <M>\mu_b \circ \mu_{b'} = \mu_{bb'}</M>.  
<P/>
</Description> 
</ManSection> 

<Example>
<![CDATA[
gap> u1 := BA1[3];
(Z(5)^0)*(1,3,5)(2,4,6)
gap> S1 := Subalgebra( A3, [ u1 ] );; 
gap> SetName( S1, "S1" );
gap> MS1 := MultiplierAlgebraOfIdealBySubalgebra( A1, I1, S1 );
<algebra of dimension 1 over GF(5)>
gap> SetName( MS1, "MS1" );
gap> BMS1 := BasisVectors( Basis( MS1 ) );; 
gap> BMS1[1];
<linear mapping by matrix, I1 -> I1>
]]>
</Example>

<ManSection>
   <Attr Name="MultiplierAlgebra"
         Arg="A" />
<Description>
The regular multipliers <M>\mu_a : A \to A</M> for all <M>a \in A</M> 
form an algebra isomorphic to <M>A</M> by the map <M>a \mapsto \mu_a</M>. 
This operation returns <C>MultiplierAlgebraOfIdealBySubalgebra(A,A,A);</C>. 
<P/>
</Description> 
</ManSection> 

<Example>
<![CDATA[
gap> MA1 := MultiplierAlgebra( A1 );
<algebra of dimension 6 over GF(5)>
gap> BMA1 := BasisVectors( Basis( MA1 ) );; 
gap> BMA1[3];
<linear mapping by matrix, <algebra-with-one of dimension 
6 over GF(5)> -> <algebra-with-one of dimension 6 over GF(5)>>
]]>
</Example>

<ManSection>
   <Attr Name="MultiplierHomomorphism"
         Arg="M" />
<Description>
If <M>M</M> is a multiplier algebra with elements of 
a subalgebra <M>B</M> of an algebra <M>A</M> 
multiplying an ideal <M>I</M> then this operation returns the 
homomorphism from <M>B</M> to <M>M</M> mapping <M>b</M> to <M>\mu_b</M>. 
</Description> 
</ManSection> 

<Example>
<![CDATA[
gap> hom1 := MultiplierHomomorphism( MA1 );;
gap> ImageElm( hom1, BA1[2] ); 
Basis( A1, [ (Z(5)^0)*(), (Z(5)^0)*(1,2,3,4,5,6), (Z(5)^0)*(1,3,5)(2\
,4,6), 
  (Z(5)^0)*(1,4)(2,5)(3,6), (Z(5)^0)*(1,5,3)(2,6,4), (Z(5)^0)*(1,6,5,4,3,2) 
 ] ) -> [ (Z(5)^0)*(1,2,3,4,5,6), (Z(5)^0)*(1,3,5)(2,4,6), 
  (Z(5)^0)*(1,4)(2,5)(3,6), (Z(5)^0)*(1,5,3)(2,6,4), (Z(5)^0)*(1,6,5,4,3,2), 
  (Z(5)^0)*() ]
]]>
</Example> 

</Section>


<Section>

<Heading>Commutative actions</Heading> 

If <M>S</M> and <M>R</M> are commutative <B>k</B>-algebras, a map 
<Display>
R \times S  ~\to~ S, 
\qquad 
(r,s)  ~\mapsto~  r \cdot s
</Display>
is a commutative action if and only if the following five axioms hold: 
<List>
<Item>
<M>k(r \cdot s) ~=~ (kr) \cdot s ~=~ r \cdot (ks)</M>, 
</Item>
<Item>
<M>r \cdot (s + s') ~=~ r \cdot s + r \cdot s', \qquad</M> 
(so <M>r \cdot 0_S = 0_S ~\forall~ r \in R</M>), 
</Item>
<Item>
<M>(r + r') \cdot s ~=~ r \cdot s + r' \cdot s, \qquad</M>  
(so <M>0_R \cdot s = 0_S ~\forall~ s \in S</M>), 
</Item>
<Item>
<M>r \cdot (ss') ~=~ (r \cdot s)s' = s(r \cdot s')</M>, 
</Item>
<Item>
<M>(rr') \cdot s ~=~ r \cdot (r' \cdot s), \qquad</M> 
(so <M>1_R \cdot s = s ~\forall~ s \in S</M> when <M>R</M> has a one), 
</Item>
</List>
for all <M>k \in </M><B>k</B>, <M>r,r' \in R</M>, and <M>s,s' \in S</M>. 
<P/> 

Notice in particular that, for fixed <M>r \in R</M>,
the map <M>s \mapsto r \cdot s</M> is a vector space homomorphism,
but not in general an algebra homomorphism.
<P/>

<ManSection>
   <Func Name="AlgebraAction"
         Arg="args" />
<Description> 
This global function calls one of the following operations, 
depending on the arguments supplied.
</Description>

</ManSection>
<ManSection>
   <Oper Name="AlgebraActionByMultipliers"
         Arg="A I B" />
<Description>
When <M>I</M> is an ideal in <M>A</M> 
and <M>B</M> is a subalgebra of <M>A</M>, 
we have seen that the multiplier homomorphism from <M>A</M> to <C>MultiplierAlgebraOfIdealBySubalgebra(A,I,B)</C> 
is an action. 
<P/> 
In the example the algebra is the group ring of the cyclic group <M>C_6</M> 
over the field <M>GF(5)</M>. 
The ideal is generated by <M>v = () + (1,3,5)(2,4,6) + (1,5,3)(2,6,4)</M>. 
The generator <M>r = (1,2,3,4,5,6)</M> acts on <M>v</M> 
by multiplication to give the vector 
<M>r \cdot v = (1,2,3,4,5,6) + (1,4)(2,5)(3,6) + (1,6,5,4,3,2)</M>,
as shown in <Ref Oper="AlgebraActionByHomomorphism"/>
</Description> 
</ManSection> 

<Example>
<![CDATA[
gap> A1 := GroupRing( GF(5), Group( (1,2,3,4,5,6) ) );;
gap> BA1 := BasisVectors( Basis( A1 ) );; 
gap> v := BA1[1] + BA1[3] + BA1[5];
(Z(5)^0)*()+(Z(5)^0)*(1,3,5)(2,4,6)+(Z(5)^0)*(1,5,3)(2,6,4)
gap> I1 := Ideal( A1, [v] );; 
gap> act1 := AlgebraActionByMultipliers( A1, I1, A1 );; 
gap> act12 := Image( act1, BA1[2] );; 
gap> Image( act12, v );
(Z(5)^0)*(1,2,3,4,5,6)+(Z(5)^0)*(1,4)(2,5)(3,6)+(Z(5)^0)*(1,6,5,4,3,2)
]]>
</Example>

<ManSection>
   <Oper Name="AlgebraActionBySurjection"
         Arg="hom" />
<Description>
Let <M>\theta : B \to A</M> be a surjective algebra homomorphism 
such that <M>\ker\theta</M> is contained in the annihilator of <M>B</M>. 
Then <M>A</M> acts on <M>B</M> by <M>a \cdot b = pb</M> 
where <M>p \in (\theta^{-1}a)</M>. 
Note that this action is well defined since 
<M>\theta^{-1}a = \{ p+k ~|~ k \in \ker\theta \}</M> 
and <M>(p+k)b = pb+kb = pb+0</M>. 
<P/>
Continuing with the previous example, 
we construct the quotient algebra <M>Q1 = A1/I1</M>, 
and the natural homomorphism <M>\theta_1 : A1 \to Q1</M>. 
The kernel of <M>\theta</M> is not contained in the annihilator of <M>A1</M>,
so an attempt to form the action fails. 
<P/>
An alternative example involves a matrix algebra <M>A_2</M>
with generator <M>m_2</M>, basis <M>\{m_2,m_2^2,m_2^3\}</M>,
and where <M>m_2^4=0</M>.
The ideal <M>I_2</M> is generated by <M>m_2^3</M>
and the quotient <M>Q_2</M> has basis <M>\{[m_2],[m_2^2]\}</M>.
</Description> 
</ManSection> 

<Example>
<![CDATA[
gap> theta1 := NaturalHomomorphismByIdeal( A1, I1 );
<linear mapping by matrix, <algebra-with-one of dimension 
6 over GF(5)> -> <algebra of dimension 4 over GF(5)>>
gap> List( BA1, v -> ImageElm( theta1, v ) ); 
[ v.1, v.2, v.3, v.4, (Z(5)^2)*v.1+(Z(5)^2)*v.3, (Z(5)^2)*v.2+(Z(5)^2)*v.4 ]
gap> AlgebraActionBySurjection( theta1 );
kernel of hom is not in the annihilator of A
fail
gap> ## an example which does not fail: 
gap> m2 := [ [0,1,2,3], [0,0,1,2], [0,0,0,1], [0,0,0,0] ];; 
gap> m2^2;
[ [ 0, 0, 1, 4 ], [ 0, 0, 0, 1 ], [ 0, 0, 0, 0 ], [ 0, 0, 0, 0 ] ]
gap> m2^3;
[ [ 0, 0, 0, 1 ], [ 0, 0, 0, 0 ], [ 0, 0, 0, 0 ], [ 0, 0, 0, 0 ] ]
gap> A2 := Algebra( Rationals, [m2] );;
gap> SetName( A2, "A2" );
gap> S2 := Subalgebra( A2, [m2^3] );; 
gap> SetName( S2, "S2" );
gap> nat2 := NaturalHomomorphismByIdeal( A2, S2 ); 
<linear mapping by matrix, A2 -> <algebra of dimension 2 over Ration\
als>>
gap> Q2 := Image( nat2 );;
gap> SetName( Q2, "Q2" );
gap> Display( nat2 );
LeftModuleHomomorphismByMatrix( Basis( A2, 
[ [ [ 0, 0, 0, 1 ], [ 0, 0, 0, 0 ], [ 0, 0, 0, 0 ], [ 0, 0, 0, 0 ] ], 
  [ [ 0, 1, 2, 3 ], [ 0, 0, 1, 2 ], [ 0, 0, 0, 1 ], [ 0, 0, 0, 0 ] ], 
  [ [ 0, 0, 1, 4 ], [ 0, 0, 0, 1 ], [ 0, 0, 0, 0 ], [ 0, 0, 0, 0 ] ] ] ), 
[ [ 0, 0 ], [ 1, 0 ], [ 0, 1 ] ], CanonicalBasis( Q2 ) )
gap> act2 := AlgebraActionBySurjection( nat2 );; 
gap> C2 := Image( act2 );;
gap> BC2 := BasisVectors( Basis( C2 ) );;
gap> b1 := BC2[1];;  b2 := BC2[2];;
gap> [ Image(b1,m2)=m2^2, Image(b1,m2^2)=m2^3, Image(b1,m2^3)=Zero(A2) ];
[ true, true, true ]
gap> [ Image(b2,m2)=m2^3, b2=b1^2 ];
[true, true ]
]]>
</Example>


<#Include Label="AlgebraActionByHomomorphism">

</Section>

<Section>

<Heading>Algebra modules</Heading> 

<#Include Label="AlgebraModules">

<#Include Label="ModuleAsAlgebra">

<#Include Label="IsModuleAsAlgebra">

<#Include Label="ModuleToAlgebraIsomorphism">

<#Include Label="AlgebraActionByModule">

</Section>

<Section>

<Heading>Actions on direct sums of algebras</Heading>

<#Include Label="DirectSumOfAlgebrasInfo">

<#Include Label="EmbeddingForDirectSumOfAlgebras">

<#Include Label="DirectSumOfAlgebraHomomorphisms">

<#Include Label="AlgebraActionOnDirectSum">

<#Include Label="DirectSumOfAlgebraActions">

</Section>

<Section>

<Heading>Other operations on algebras</Heading>

<ManSection>
   <Oper Name="SemidirectProductOfAlgebras"
         Arg="R act S" />
<Description>
When <M>R,S</M> are commutative algebras and <M>R</M> acts on <M>S</M> 
then we can form the semidirect product <M>R \ltimes S</M>, 
where the product is given by: 
<Display>
(r_1,s_1)(r_2,s_2) ~=~ (r_1r_2,~ r_1 \cdot s_2 + r_2 \cdot s_1 + s_1s_2). 
</Display> 

This product, as well as being commutative, is associative: 
<M>(r_1,s_1)(r_2,s_2)(r_3,s_3)</M> expands as: 
<Display>
(r_1r_2r_3,~ \left (r_1r_2)\cdot s3 + (r_1r_3)\cdot s_2 + (r_2r_3)\cdot s_1 
            + r_1 \cdot (s_2s_3) + r_2 \cdot (s_1s_3) + r_3 \cdot (s_1s_2) 
            + s_1s_2s_3 \right).
</Display> 

If <M>B_R, B_S</M> are the sets of basis vectors for <M>R</M> and <M>S</M> 
then <M>R \ltimes S</M> has basis 
<Display>
\{(r,0_S) ~|~ r \in B_R\} ~\cup~ \{(0_R,s) ~|~ s \in B_S\} 
</Display>
with defining products 
<Display>
(r_1,0_S)(r_2,0_S) = (r_1r_2,0_S), \qquad 
(r,0_S)(0_R,s) = (0_R,r \cdot s), \qquad 
(0_R,s_1)(0_R,s_2) = (0_R,s_1s_2). 
</Display>
Continuing the example above,
</Description> 
</ManSection> 

<Example>
<![CDATA[
gap> P1 := SemidirectProductOfAlgebras( A1, act1, I1 ); 
<algebra of dimension 8 over GF(5)>
gap> Embedding( P1, 1 );
[ (Z(5)^0)*(), (Z(5)^0)*(1,2,3,4,5,6), (Z(5)^0)*(1,3,5)(2,4,6), 
  (Z(5)^0)*(1,4)(2,5)(3,6), (Z(5)^0)*(1,5,3)(2,6,4), (Z(5)^0)*(1,6,5,4,3,2) 
 ] -> [ v.1, v.2, v.3, v.4, v.5, v.6 ]
gap> Embedding( P1, 2 );
[ (Z(5)^0)*()+(Z(5)^0)*(1,3,5)(2,4,6)+(Z(5)^0)*(1,5,3)(2,6,4), 
  (Z(5)^0)*(1,2,3,4,5,6)+(Z(5)^0)*(1,4)(2,5)(3,6)+(Z(5)^0)*(1,6,5,4,3,2) ] -> 
[ v.7, v.8 ]
gap> Projection( P1, 1 );
[ v.1, v.2, v.3, v.4, v.5, v.6, v.7, v.8 ] -> 
[ (Z(5)^0)*(), (Z(5)^0)*(1,2,3,4,5,6), (Z(5)^0)*(1,3,5)(2,4,6), 
  (Z(5)^0)*(1,4)(2,5)(3,6), (Z(5)^0)*(1,5,3)(2,6,4), (Z(5)^0)*(1,6,5,4,3,2), 
  <zero> of ..., <zero> of ... ]
gap> P2 := SemidirectProductOfAlgebras( Q2, act2, A2 );
Q2 |X A2
gap> Embedding( P2, 1 );
[ v.1, v.2 ] -> [ v.1, v.2 ]
gap> Embedding( P2, 2 );
[ [ [ 0, 1, 2, 3 ], [ 0, 0, 1, 2 ], [ 0, 0, 0, 1 ], [ 0, 0, 0, 0 ] ], 
  [ [ 0, 0, 1, 4 ], [ 0, 0, 0, 1 ], [ 0, 0, 0, 0 ], [ 0, 0, 0, 0 ] ], 
  [ [ 0, 0, 0, 1 ], [ 0, 0, 0, 0 ], [ 0, 0, 0, 0 ], [ 0, 0, 0, 0 ] ] ] -> 
[ v.3, v.4, v.5 ]
]]>
</Example> 

<ManSection>
   <Attr Name="SemidirectProductOfAlgebrasInfo"
         Arg="P" />
<Description>
The <C>SemidirectProductOfAlgebrasInfo(P)</C> for <M>P = R \ltimes S</M> 
is a record with fields <C>P.action</C>; <C>P.algebras</C>; 
<C>P.embeddings</C>; and <C>P.projections</C>. 
</Description> 
</ManSection> 

</Section>

<Section Label="algebra-homomorphism-lists">

<Heading>Lists of algebra homomorphisms</Heading> 

<ManSection>
   <Oper Name="AllAlgebraHomomorphisms"
         Arg="A B" />
   <Oper Name="AllBijectiveAlgebraHomomorphisms"
         Arg="A B" />
   <Oper Name="AllIdempotentAlgebraHomomorphisms"
         Arg="A B" />
<Description>
These three operations list all the homomorphisms from <M>A</M> to <M>B</M> 
of the specified type. 
These lists can get very long, so the operations should only be used with 
small algebras. 
</Description> 
</ManSection> 

<Example>
<![CDATA[
gap> A2c6 := GroupRing( GF(2), Group( (1,2,3,4,5,6) ) );;
gap> R2c3 := GroupRing( GF(2), Group( (7,8,9) ) );;
gap> homAR := AllAlgebraHomomorphisms( A2c6, R2c3 );;
gap> List( homAR, h -> MappingGeneratorsImages(h) );
[ [ [ (Z(2)^0)*(1,6,5,4,3,2) ], [ <zero> of ... ] ], 
  [ [ (Z(2)^0)*(1,6,5,4,3,2) ], [ (Z(2)^0)*() ] ], 
  [ [ (Z(2)^0)*(1,6,5,4,3,2) ], [ (Z(2)^0)*()+(Z(2)^0)*(7,8,9) ] ], 
  [ [ (Z(2)^0)*(1,6,5,4,3,2) ], 
      [ (Z(2)^0)*()+(Z(2)^0)*(7,8,9)+(Z(2)^0)*(7,9,8) ] ], 
  [ [ (Z(2)^0)*(1,6,5,4,3,2) ], [ (Z(2)^0)*()+(Z(2)^0)*(7,9,8) ] ], 
  [ [ (Z(2)^0)*(1,6,5,4,3,2) ], [ (Z(2)^0)*(7,8,9) ] ], 
  [ [ (Z(2)^0)*(1,6,5,4,3,2) ], [ (Z(2)^0)*(7,8,9)+(Z(2)^0)*(7,9,8) ] ], 
  [ [ (Z(2)^0)*(1,6,5,4,3,2) ], [ (Z(2)^0)*(7,9,8) ] ] ]
gap> homRA := AllAlgebraHomomorphisms( R2c3, A2c6 );;
gap> List( homRA, h -> MappingGeneratorsImages(h) );
[ [ [ (Z(2)^0)*(7,8,9) ], [ <zero> of ... ] ], 
  [ [ (Z(2)^0)*(7,8,9) ], [ (Z(2)^0)*() ] ], 
  [ [ (Z(2)^0)*(7,8,9) ], [ (Z(2)^0)*()+(Z(2)^0)*(1,3,5)(2,4,6) ] ], 
  [ [ (Z(2)^0)*(7,8,9) ], 
      [ (Z(2)^0)*()+(Z(2)^0)*(1,3,5)(2,4,6)+(Z(2)^0)*(1,5,3)(2,6,4) ] ], 
  [ [ (Z(2)^0)*(7,8,9) ], [ (Z(2)^0)*()+(Z(2)^0)*(1,5,3)(2,6,4) ] ], 
  [ [ (Z(2)^0)*(7,8,9) ], [ (Z(2)^0)*(1,3,5)(2,4,6) ] ], 
  [ [ (Z(2)^0)*(7,8,9) ], [ (Z(2)^0)*(1,3,5)(2,4,6)+(Z(2)^0)*(1,5,3)(2,6,4) ] 
     ], [ [ (Z(2)^0)*(7,8,9) ], [ (Z(2)^0)*(1,5,3)(2,6,4) ] ] ]
gap> bijAA := AllBijectiveAlgebraHomomorphisms( A2c6, A2c6 );;
gap> List( bijAA, h -> MappingGeneratorsImages(h) );
[ [ [ (Z(2)^0)*(1,6,5,4,3,2) ], 
      [ (Z(2)^0)*()+(Z(2)^0)*(1,3,5)(2,4,6)+(Z(2)^0)*(1,4)(2,5)(3,6) ] ], 
  [ [ (Z(2)^0)*(1,6,5,4,3,2) ], 
      [ (Z(2)^0)*()+(Z(2)^0)*(1,4)(2,5)(3,6)+(Z(2)^0)*(1,5,3)(2,6,4) ] ], 
  [ [ (Z(2)^0)*(1,6,5,4,3,2) ], [ (Z(2)^0)*(1,2,3,4,5,6) ] ], 
  [ [ (Z(2)^0)*(1,6,5,4,3,2) ], 
      [ (Z(2)^0)*(1,2,3,4,5,6)+(Z(2)^0)*(1,3,5)(2,4,6)+(Z(2)^0)*(1,5,3)
            (2,6,4) ] ], 
  [ [ (Z(2)^0)*(1,6,5,4,3,2) ], 
      [ (Z(2)^0)*(1,3,5)(2,4,6)+(Z(2)^0)*(1,5,3)(2,6,4)+(Z(2)^0)*
            (1,6,5,4,3,2) ] ], 
  [ [ (Z(2)^0)*(1,6,5,4,3,2) ], [ (Z(2)^0)*(1,6,5,4,3,2) ] ] ]
gap> ideAA := AllIdempotentAlgebraHomomorphisms( A2c6, A2c6 );; 
gap> Length( ideAA );
14
]]>
</Example>

</Section> 

</Chapter>
